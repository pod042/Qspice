*******************************************************************
******STMicroelectronics MOSFET, IGBT and Bipolar Library *********
*******************************************************************
*											*
* Models provided by STMicroelectronics are not guaranteed to	*
* fully represent all the specifications and operating		*
* characteristics of the product behavior that they reproduce.	*
* The model describes the characteristics of a typical device.	*
* In all cases, the current product data sheet contains all		*
* information to be used like final design guidelines and the	*
* only actual performance specification.					*
* Altough models can be a useful tool in evaluating device		*
* performance, they cannot model exact device performance under	*
* all conditions.									*
* STMicroelectronics therefore does not assume any			*
* responsibility arising from their use.					*
* STMicroelectronics reserves the right to change models		*
* without prior notice.								*
*											*
* Note: The model doesn't take into account the drain, gate,	*
* source inductances.If these contributions have to be		*
* considered it is possible include the inductors externally.	*
* For this package the values can be estimated as follow		*
*											*
* Ldrain= 1nH ,Lsource=2nH and Lgate=2.5nH				*
*											*
* Rev 1.0 - 15 May 2013		      				*
*****************************************************************

********************************************************
.SUBCKT STGB20V60F-V3 1 2 3 Tj Tcase 



********************************DIODO AK ********************************

V_bjt 1 1z 0

G_anodo1 1z a1  VALUE={I_diode(V(1z,a1),v(Tj))}
r_escusione 1z a1 500

G_Anodo 1y a1 VALUE={(V(1y,a1)/Rdiodo(V(TJ)))}
r_conv1 1y a1 10


.FUNC I_diode(z3,k3) {(exp(min(-15+z3/(did(k3)),7))-1n)}

.FUNC did(k4) {0.078+50E-06*k4}

.FUNC Rdiodo(rdid) {rdid0*((rdid+273)/300)**(rdid_temp)}


.PARAM rdid0={0.0001} rdid_temp=1

.PARAM  kpsat0=105 kplin0=105 drs=80m


***********************************************************



************************* Itail_1 *********************************
R_R003 aa 0 500E06
R_edep 1y d_dedep 10
G_edep a1 d VALUE={(v(1z,d_dedep))/(200-0.66*v(tj))}
E_Eds d_dedep edep VALUE {-V(aa,0)*2}
C_Cds edep 3 20E-12
E_E001 ba 0 VALUE {-0.2*((V(Tj)))*I(V_BJT)**1.7}
R_R001 aa ba 1
C aa 0 1E-12
***********************************************************

*******************************************************************
*E1 Tj val_T VALUE={TEMP}
*R1 val_T 0 1E-03
***********************************************************
Rtk Tj 0 1E10
Rtk1 Tj 0 1E10
************************************
VLd 1y d1k 0
VLs ss 3c 0
VLG 2 g2 0
*rg1 g2 g {rg}
Grg1 g2 g VALUE ={V(g2,g)/(0.0025*V(tj)+1.19)}
*******************************************************************
.PARAM Area=0.4  
***************************************************
.PARAM  rg=1.65 Vth0=7.5
.PARAM  lambda=0.0000071 


.PARAM  rd=2.7m 
 

***********************************
.PARAM  unt =-1.05 vthx=5.5m ksat=-0.2 klin= -1
.PARAM  b=1 

E_soglia soglia 0 value {vth1(V(TJ))}
R_soglia soglia 0 1


***********************************************************
.FUNC r_s(T) {((T+273)/300)**(unt)/drs}
.FUNC vth1(x) {vth0-vthx*(x-27)}
.FUNC kpsat(x) {kpsat0*((x+273)/300)**(ksat)}
.FUNC kplin(x) {kplin0*((x+273)/300)**(klin)}
.FUNC un(T) {b*((T+273)/300)**(unt)}
R_lambda L 0 1
V_lambda L 0 {lambda}
***********************************************************
***********************************************************
.PARAM H=1

Gmos d s value {Area*(un(V(TJ)))*(IF(v(g,s)<vth1(V(TJ)),0,
+(IF((V(d,s)<(v(g,s)-vth1(V(TJ)))*kpsat(v(TJ))/kplin(v(TJ))),(1+LAMBDA*v(d,s))*kplin(v(TJ))/2*v(d,s)*
+(2*(v(g,s)-vth1(V(TJ)))-kplin(v(TJ))/kpsat(v(TJ))*v(d,s)),
+(1+LAMBDA*v(d,s))*kpsat(v(TJ))/2*(v(g,s)-vth1(V(TJ)))**2))))}


*G_RMos d1k dd VALUE={Area*((un(V(TJ))*(V(d1k,d)))/(rd+rpa*((I(V_sense)**a))**Rx))}
Vdc_Grmos d1k dd 0

V_grss s ss 0
G_rs 3c 3 VALUE={(Area*(r_s(V(TJ))*v(3c,3))/drs)}

R_Grs 3c 3 1E03

R_Rmos d1k dd 1E03
V_sense dd d 0
***********************************************************
rdd dd 0 1E10
rdd1 d 0 1E10
rdd3 s 0 1E10
R_DS d s 1E10
***********************************************************
CGS g 3  {6400p*Area}
R_CGS g 3 500E06
***********************************************************
Cref 40 0 1E-12
E2 40 50 value {V(g,d1k)}
V2 50 0 0
Gcdg d1k g value {1.4*Area*V(alfa)*i(V2)*1E12}
Rcap 0 alfa 1E03
Ecap alfa 0 TABLE = {V(d1k,3)}
+(0.1,9332.42p) 
+(0.2,8556.4p) 
+(0.5,7842.24p) 
+(1,7749.27p) 
+(2,5638.31p) 
+(3,4997.32p) 
+(5,3376.06p) 
+(7,2315.57p) 
+(10,1862.77p) 
+(15,1314.43p) 
+(20,885.72p) 
+(25,566.43p) 
+(30,352.15p) 
+(40,132.2p) 
+(50,118.64p) 
+(100,85.03p) 
+(150,70.01p) 
+(200,61.07p) 
+(250,54.94p) 
+(300,50.46p) 
+(350,47.03p) 
+(400,44.41p) 
+(450,43.99p) 
+(500,44.35p) 
+(550,44.84p) 
+(600,44.93p) 

***********************************************************
Cref2 402 0 1E-12
E22 402 502 value {V(ss,d1k)}
V22 502 0 0

Rsensing d1k d1kx 1m
Vsensing d1kx d1kxx 0
Gtail d1k ss value {1*I(Vsensing)*(0.018*v(tj)+1.5)}

***********************************************************

Gcdg2 d1kxx ss value {6*Area*V(alfa2)*i(V22)*1E12}
Rcap2 0 alfa2 100E03
Ecap2 alfa2 0 TABLE = {V(d1k,ss)}
+(0.1,5733.01p) 
+(0.2,5114.49p) 
+(0.5,2626.09p) 
+(1,1413.93p) 
+(2,919.08p) 
+(3,735.7p) 
+(5,669.93p) 
+(7,584.26p) 
+(10,507.75p) 
+(15,435.44p) 
+(20,493.06p) 
+(25,463.7p) 
+(30,441.84p) 
+(40,411.09p) 
+(50,390.17p) 
+(100,338.42p) 
+(150,317.29p) 
+(200,304.24p) 
+(250,295.22p) 
+(300,288.4p) 
+(350,282.95p) 
+(400,278.72p) 
+(450,276.95p) 
+(500,276.04p) 
+(550,275.32p) 
+(600,274.64p) 


************************BVCES******************************
V_BVCES   1z BVCES1 0
R_bvdss	BVCES1 d1bvdss1 1
V_bvdss	d1bvdss1 d1bvdss2 0
G_bvd	d1bvdss2 3 VALUE={I_BVDSS(V(d1bvdss2,3),v(Tj))}
R_GBDSS	d1bvdss2 0 1E9
.FUNC I_BVDSS(z,k1) {(exp(min(-355+z/(bvd(k1)),10))-250n)}
.FUNC bvd(k) {1.9+0.00170*k}

*****************************************************
***********************************************************
E_Pw thx 0 VALUE ={I(V_bjt)*v(1,3)}
R_power thx 0 1E6
RTHXX thx thxx 1E3
CTXX thxx 0 1E-09

G_pw_igbt 0 TJ VALUE ={Limit(v(thxx),0,100E3)}


R_Gpower 0 TJ 1E6
***************************************************


Rth1 Tj  Th1    0.788E-02
Rth2 Th1 Th2    1.80E-01
Rth3 Th2 Th3    1.15E-01
Rth4 Th3 Th4    6.0E-01
Rth5 Th4 Tcase  0.00001

Cth1 Tj  0   590u
Cth2 Th1 0   0.8m
Cth3 Th2 0   20m
Cth4 Th3 0   11m
Cth5 Th4 0   60m
Cth6 Tcase 0 0.15


.ENDS STGB20V60F-V3

*$
